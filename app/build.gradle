apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("private/keystore.properties");
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        config {
            storeFile file(keystoreProperties['storeFile'])
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 25
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId "org.beiwe.app"
        minSdkVersion 18
        targetSdkVersion 23
        versionCode 24       //TODO: update this Beiwe version code for new releases
        versionName '2.2.5'  //TODO: update this Beiwe version number for new releases
        setProperty("archivesBaseName", "Beiwe-$versionName")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.config
            debuggable true
            manifestPlaceholders = [appNameSuffix: ""]
            buildConfigField("boolean", "APP_IS_BETA", "false")
            buildConfigField("boolean", "APP_IS_DEV", "false")
        }
        beta {
            versionNameSuffix("-beta")
            signingConfig signingConfigs.config
            debuggable true
            manifestPlaceholders = [appNameSuffix: "-beta"]
            buildConfigField("boolean", "APP_IS_BETA", "true")
            buildConfigField("boolean", "APP_IS_DEV", "false")
        }
        development {
            versionNameSuffix("-development")
            signingConfig signingConfigs.config
            debuggable true
            manifestPlaceholders = [appNameSuffix: "-development"]
            buildConfigField("boolean", "APP_IS_BETA", "true")
            buildConfigField("boolean", "APP_IS_DEV", "true")
        }
    }
    flavorDimensions "customizableServerUrl"
    productFlavors {
        googlePlayStore {
            dimension "customizableServerUrl"
            manifestPlaceholders = [appName: "Beiwe2"]
            buildConfigField("boolean", "CUSTOMIZABLE_SERVER_URL", 'true')
        }
        onnelaLabServer {
            dimension "customizableServerUrl"
            manifestPlaceholders = [appName: "Beiwe"]
            buildConfigField("boolean", "CUSTOMIZABLE_SERVER_URL", "false")
        }
    }
    useLibrary "org.apache.http.legacy"
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}
// Don't include the default "debug" build variant as an option
android.variantFilter { variant ->
    if(variant.buildType.name.equals('debug')) {
        variant.setIgnore(true);
    }
}

repositories {
    maven {
        url "https://s3.amazonaws.com/repo.commonsware.com"
    }
}

dependencies {
    implementation 'com.google.android.gms:play-services-maps:11.8.0'
    implementation 'com.google.android.gms:play-services-places:11.8.0'
    implementation 'com.android.support:support-v4:25.4.0'
    implementation 'com.android.support:recyclerview-v7:25.4.0'
    compile 'com.google.android.gms:play-services-location:11.8.0'
    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.uber.sdk:rides-android:0.5.3'
    compile 'com.commonsware.cwac:anddown:0.3.0'
}
